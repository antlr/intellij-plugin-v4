buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "1.4.0"
}

wrapper {
    gradleVersion = '6.8'
}

group 'antlr'
version pluginVersion

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'antlr'

compileJava {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

intellij {
    version = ideaVersion

    pluginName = 'antlr-intellij-plugin-v4'
    downloadSources = true
    updateSinceUntilBuild = false
}

repositories {
    mavenCentral()
}

dependencies {
    antlr("org.antlr:antlr4:$antlr4Version") { // use ANTLR version 4
        exclude group:'com.ibm.icu', module:'icu4j'
    }
    implementation "org.antlr:antlr4-intellij-adaptor:0.1"
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.3.1'
}

generateGrammarSource {
    include("**/ANTLRv4*.g4")

    arguments += [
            "-package", "org.antlr.intellij.plugin.parser",
            "-lib", "src/main/antlr/org/antlr/intellij/plugin/parser",
            "-Xexact-output-dir"
    ]
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}